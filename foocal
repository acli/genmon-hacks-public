#!/usr/bin/perl
# vi:set sw=2 ai sm:
#
# Copyright 2016-2018 by Ambrose Li <ambrose.li@gmail.com>
# The primary purpose of this script is to figure out the year number in the Chinese system
# so that I can name my CV's without having to look up two web pages.
# Everything else is just for kicks =P
#
# This script doesn't really do a lot of calculations itself; it just gathers the information
# from elsewhere (e.g., hebcal, or the HKO) and presents it.
#
# http://www.hko.gov.hk/gts/time/calendar/text/T2016e.txt
# http://www.hko.gov.hk/gts/time/calendar/text/T2016c.txt
# www.hko.gov.hk/gts/time/stemsandbranches.htm
#
# https://scitechvista.nat.gov.tw/zh-tw/articles/c/9/7/10/1/998.htm

use strict;
use integer;
use utf8;

use POSIX;
use DateTime;

use vars qw( $use_event_sunrise );
BEGIN {
  if (!eval { require DateTime::Astro::Sunrise; }) {
    $use_event_sunrise = 1;
    require DateTime::Event::Sunrise;
  }
}
sub sunrise_new ($$$$) { my($a, $b, $c, $d) = @_; return $use_event_sunrise? DateTime::Event::Sunrise->new($a, $b, $c, $d): DateTime::Astro::Sunrise->new($a, $b, $c, $d); }

use Getopt::Long;
use Data::Dumper;

use vars qw( $fake );
use vars qw( $debug );
use vars qw( $enable_seconds );
use vars qw( $enable_qing_quarters );
use vars qw( $enable_planetary $planetary_omit_details );
use vars qw( $enable_chinese $lang );
use vars qw( $enable_decaminutes $enable_minutes );
use vars qw( $decimals );
use vars qw( $sysline_mode );
$decimals = 2;

use vars qw( $representation );
$representation = 'name';

use vars qw( $lat $lon );

use vars qw( @planets );
@planets = (
  {
    'name' => {'zh' => '日', 'en' => 'Sun'},
    'icon' => '☉',
  },
  {
    'name' => {'zh' => '月', 'en' => 'Moon'},
    'icon' => '☽',
  },
  {
    'name' => {'zh' => '火', 'en' => 'Mars'},
    'icon' => '♂',
  },
  {
    'name' => {'zh' => '水', 'en' => 'Mercury'},
    'icon' => '☿',
  },
  {
    'name' => {'zh' => '木', 'en' => 'Jupiter'},
    'icon' => '♃',
  },
  {
    'name' => {'zh' => '金', 'en' => 'Venus'},
    'icon' => '♀',
  },
  {
    'name' => {'zh' => '土', 'en' => 'Saturn'},
    'icon' => '♄',
  },
);

use vars qw( @stems @branches );
@stems = (
  {
    'zh' => '甲',
    'en' => 'Jia',
  },
  {
    'zh' => '乙',
    'en' => 'Yi',
  },
  {
    'zh' => '丙',
    'en' => 'Bing',
  },
  {
    'zh' => '丁',
    'en' => 'Ding',
  },
  {
    'zh' => '戊',
    'en' => 'Wu',
  },
  {
    'zh' => '己',
    'en' => 'Ji',
  },
  {
    'zh' => '庚',
    'en' => 'Geng',
  },
  {
    'zh' => '辛',
    'en' => 'Xin',
  },
  {
    'zh' => '壬',
    'en' => 'Ren',
  },
  {
    'zh' => '癸',
    'en' => 'Gui',
  },
);
# Note that apparently the hours have names in the Han dynasty
# Cf. http://www.oldkids.cn/group/post_detail.php?did=669531
@branches = (
  {
    'zh' => '子',
    'en' => 'Zi',
    'name' => {'zh' => '鼠', 'en' => 'Mouse'},
    'icon' => '🐁',
    'han_hour' => '夜半',
  },
  {
    'zh' => '丑',
    'en' => 'Chou',
    'name' => {'zh' => '牛', 'en' => 'Cow'},
    'icon' => '🐄',
    'han_hour' => '雞鳴',
  },
  {
    'zh' => '寅',
    'en' => 'Yin',
    'name' => {'zh' => '虎', 'en' => 'Tiger'},
    'icon' => '🐅',
    'han_hour' => '平旦',
  },
  {
    'zh' => '卯',
    'en' => 'Mao',
    'name' => {'zh' => '兔', 'en' => 'Rabbit'},
    'icon' => '🐇',
    'han_hour' => '日出',
  },
  {
    'zh' => '辰',
    'en' => 'Chen',
    'name' => {'zh' => '龍', 'en' => 'Dragon'},
    'icon' => '🐉',
    'han_hour' => '食時',
  },
  {
    'zh' => '巳',
    'en' => 'Si',
    'name' => {'zh' => '蛇', 'en' => 'Snake'},
    'icon' => '🐍',
    'han_hour' => '隅中',
  },
  {
    'zh' => '午',
    'en' => 'Wu',
    'name' => {'zh' => '馬', 'en' => 'Horse'},
    'icon' => '🐎',
    'han_hour' => '日中',
  },
  {
    'zh' => '未',
    'en' => 'Wei',
    'name' => {'zh' => '羊', 'en' => 'Sheep'},
    'icon' => '🐐',
    'han_hour' => '日昳',
  },
  {
    'zh' => '申',
    'en' => 'Shen',
    'name' => {'zh' => '猴', 'en' => 'Monkey'},
    'icon' => '🐒',
    'han_hour' => '晡時',
  },
  {
    'zh' => '酉',
    'en' => 'You',
    'name' => {'zh' => '雞', 'en' => 'Chicken'},
    'icon' => '🐔',
    'han_hour' => '日入',
  },
  {
    'zh' => '戌',
    'en' => 'Xu',
    'name' => {'zh' => '狗', 'en' => 'Dog'},
    'icon' => '🐕',
    'han_hour' => '黄昏',
  },
  {
    'zh' => '亥',
    'en' => 'Hai',
    'name' => {'zh' => '豬', 'en' => 'Pig'},
    'icon' => '🐖',
    'han_hour' => '人定',
  },
);

# We use the weird "meaningful" names found in https://zh.wikipedia.org/wiki/希伯來曆
# because they are short
use vars qw( %hebrew_months );
%hebrew_months = (
  'Nisan' => {
    'zh' => '樂',
    'zh' => '七',
    'en' => 'Nisan',
    'xx' => '7',
  },
  'Iyyar' => {
    'zh' => '愛',
    'zh' => '八',
    'en' => 'Iyyar',
    'xx' => '8',
  },
  'Sivan' => {
    'zh' => '築',
    'zh' => '九',
    'en' => 'Sivan',
    'xx' => '9',
  },
  'Tamuz' => {
    'zh' => '穫',
    'zh' => '十',
    'en' => 'Tammuz',
    'xx' => '10',
  },
  'Av' => {
    'zh' => '熟',
    'zh' => '11',
    'en' => 'Av',
    'xx' => '11',
  },
  'Elul' => {
    'zh' => '酹',
    'zh' => '12',
    'en' => 'Elul',
    'xx' => '12',
  },
  'Tishrei' => {
    'zh' => '施',
    'zh' => '正',
    'en' => 'Tishrei',
    'xx' => '1',
  },
  'Cheshvan' => {
    'zh' => '甦',
    'zh' => '二',
    'en' => 'Cheshvan',
    'xx' => '2',
  },
  'Kislev' => {
    'zh' => '孕',
    'zh' => '三',
    'en' => 'Kislev',
    'xx' => '3',
  },
  'Tevet' => {
    'zh' => '息',
    'zh' => '四',
    'en' => 'Tevet',
    'xx' => '4',
  },
  "Sh'vat" => {
    'zh' => '洪',
    'zh' => '五',
    'en' => 'Shevat',
    'xx' => '5',
  },
  'Adar I' => {
    'zh' => '閏魔',
    'zh' => '閏六',
    'en' => 'Adar I',
    'xx' => '6a',
  },
  'Adar' => {
    'zh' => '魔',
    'zh' => '六',
    'en' => 'Adar II',
    'xx' => '6b',
  },
);

sub stem_branch_name ($) {
  my($year) = @_;
  $year -= 1;
  $year %= 60;
  return sprintf('%s%s%s', $stems[$year%10]->{$lang}, ($enable_chinese? '': '-'), $branches[$year%12]->{$lang});
}

sub get_representation ($) {
  my($s) = @_;
  return $representation =~ /^icon/? $s->{'icon'}: $s->{'name'}->{$lang};
}

sub start_of_day ($) {
  my($t) = @_;
  my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime $t;
  return mktime(0, 0, 0, $mday, $mon, $year);
}

sub yesterday ($) {
  my($t) = @_;
  return start_of_day(start_of_day($t) - 12*3600);
}

sub tomorrow ($) {
  my($t) = @_;
  return start_of_day(start_of_day($t) + 36*3600);
}

sub estimate_sunrise ($) {
  my($t) = @_;
  my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime $t;
  my $dt = DateTime->new(year => $year + 1900, month => $mon + 1, day => $mday);
  my $sunrise = sunrise_new($lat, $lon, undef, 1);
  my $tmp = ($sunrise->sunrise($dt))[0];
  return $tmp->strftime('%s');
}

sub estimate_sunset ($) {
  my($t) = @_;
  my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime $t;
  my $dt = DateTime->new(year => $year + 1900, month => $mon + 1, day => $mday);
  my $sunrise = sunrise_new($lat, $lon, undef, 1);
  my $tmp = ($sunrise->sunrise($dt))[1];
  return $tmp->strftime('%s');
}

sub trim ($) {
  my($s) = @_;
  $s =~ s/^\s+//;
  $s =~ s/\s+$//;
  return $s;
}

sub ordinal ($) {
  my($n) = @_;
  my $det1 = $n%10;
  my $det2 = int($n/10)%10;
  return sprintf '%d%s', $n, ($det2 == 1? 'th': $det1 == 1? 'st': $det1 == 2? 'nd': $det1 == 3? 'rd': 'th');
}

use vars qw( %chinese_digits );
%chinese_digits = (
  '一' => 1,
  '二' => 2,
  '三' => 3,
  '四' => 4,
  '五' => 5,
  '六' => 6,
  '七' => 7,
  '八' => 8,
  '九' => 9,
  '十' => 10,
  '廿' => 20,
  '卅' => 30,
);
sub parse_chinese_number ($) {
  my($s) = @_;
  my($tens, $units) = ($chinese_digits{$1}, $chinese_digits{$2}) if $s =~ /^(\S?)(\S)$/;
  return ($units == 10 && defined $tens)? $tens*10: $tens + $units;
}

sub read_ini ($) {
  my($input) = @_;
  my $it;
  local(*INPUT, $.);
  if (open(INPUT, '<:utf8', $input)) {
    for (my $section;;) {
      my $s = scalar <INPUT>;
    last unless defined $s;
      chomp $s;
      if ($s =~ /^\s*(?:[#\%;]|$)/) {
	;
      } elsif ($s =~ /^\s*\[\s*([^\[\]]+?)\s*\]\s*$/) {
	$section = $1;
      } elsif ($s =~ /^\s*([^=]+?)\s*=\s*(.*)/) {
	if (defined $section) {
	  $it->{$section}->{$1} = $2;
	} else {
	  $it->{$1} = $2;
	}
      } else {
	warn "$input:$.: Warning: Unknown input \"$s\"\n";
      }
    }
    close INPUT;
  }
  return $it;
}

sub read_xfce_cache () {
  my $input = "$ENV{HOME}/.config/xfce4/panel/weather-17.rc";
  my $dir = "$ENV{HOME}/.cache/xfce4/weather";
  my $it;
  local(*DIR);
  my $rc = read_ini $input;
  if (defined $rc) {
    ($lat, $lon) = ($rc->{'lat'}, $rc->{'lon'});
    $input = sprintf('%s/weatherdata_%s_%s_%s', $dir, $lat, $lon, $rc->{'msl'});
    my $data = read_ini $input;
    for (my $i = 0;; $i += 1) {
      my $key = "astrodata$i";
      my $det = $data->{$key};
    last unless defined $det;
      my($yyyy, $mm, $dd) = ($1, $2, $3) if $det->{'day'} =~ /^(\d\d\d\d)-(\d\d)-(\d\d)$/;
      if (defined $dd) {
	my $midnight = mktime(0, 0, 0, $dd, $mm - 1, $yyyy - 1900);
	if ($midnight) {
	  for my $key (keys %$det) {
	    my $val = $det->{$key};
	    if ($val =~ /^(\d\d\d\d)-(\d\d)-(\d\d)T(\d\d):(\d\d):(\d\d)Z$/) {
	      $val = mktime($6, $5, $4, $3, $2 - 1, $1 - 1900);	# mktime assumes local time so the result is wrong
	      die "Internal error: mktime failed for \"$val\"\n" unless defined $val;
	      my $z = POSIX::strftime('%z', localtime time);	# try to figure out timezone offset
	      if ($z =~ /^-(\d\d)(\d\d)$/) {
		$val -= $1 * 3600 + $2 * 60;
	      } elsif ($z =~ /^\+(\d\d)(\d\d)$/) {
		$val += $1 * 3600 + $2 * 60;
	      } else {
		die "Internal error: Unknown timezone offset \"$z\"\n";
	      }
	    } elsif ($val eq 'true') {
	      $val = 1;
	    } elsif ($val eq 'false') {
	      $val = 0;
	    }
	    $it->{$midnight}->{$key} = $val;
	  }
	}
      }
    }
  }
  return $it;
}

sub read_page ($;$) {
  my($url, $cache) = @_;
  my $it;
  my $cache_valid = defined $cache && -f $cache;
  my $input = $cache_valid? $cache: '-|';
  my $output = $cache_valid? '-': $cache;
  my @cmd = ('/usr/bin/wget', '-q', '-O', $output, $url);
  my $pid = open(INPUT, $input);
  die "$0: read_page: fork: $!\n" if !$cache_valid && !defined $pid;
  @cmd = (@cmd, '-U', $fake) if defined $fake;
  if ($cache_valid || (defined $pid && $pid)) {
    binmode(INPUT, ':encoding(BIG5)') if $enable_chinese;
    for (;;) {
      my $s = scalar <INPUT>;
    last unless defined $s;
      $it .= $s;
    }
    close INPUT;
  } elsif (defined $pid) {
    exec {$cmd[0]} @cmd;
    die "$0: read_page: exec: $!\n";
  }
  return $it;
}

sub read_cached_page ($;$) {
  my($url, $cache) = @_;
  return read_page $url, $cache;
}

sub read_hko_conversion_table () {
  my $t = time;
  my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime $t;
  my $dir = "$ENV{HOME}/Documents/calendar";
  my $basename = sprintf('T%04d%s.txt', $year + 1900, ($enable_chinese? 'c': 'e'));
  my $input = "$dir/$basename";
  mkdir $dir unless -d $dir;
  return [read_cached_page("http://www.hko.gov.hk/gts/time/calendar/text/$basename", $input), $t];
}

# Quarters are weird. See https://zh.wikipedia.org/wiki/%E5%88%BB
#                     and http://yywz.snnu.edu.cn/show.aspx?id=697&cid=26
# Apparently the old Chinese system also used proportional hours... so we'll need to refactor things here at some point
# http://www.twword.com/wiki/%E5%B9%B2%E6%94%AF%E7%B4%80%E6%B3%95
sub determine_chinese_date () {
  my($data, $t) = @{( read_hko_conversion_table )};
  my $it;
  my($header, $headings, @data) = split(/\r?\n/, $data);
  my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime $t;
  my $midnight = start_of_day $t;
  my $deciday;
  ($headings, @data) = @data if !$headings; # Chinese data file
  if ($hour == 23) { # Zi is 23:00-00:59, so 23:xx is technically already the next day =P
    (my $_1, my $_2, my $_3, $mday, $mon, $year) = localtime($t + 3600);
  }
  my $data = read_xfce_cache;
  if (defined $data) {
    my $t = time;
    my $today = start_of_day $t;
    my $yesterday = yesterday $t;
    my $tomorrow = tomorrow $t;
    my $det = $data->{$today};
    my($daybreak, $slice_start, $slice_end, $starting_hour);
    # Proportional hour is used in a very early system (Spring and Autumn Period / Warring States),
    # but I don't even have an idea when the day starts (probably day before night) so we'll just use an arbitary cutoff
    if ($t < $data->{$today}->{'sunrise'}) {
      $daybreak = $data->{$yesterday}->{'sunrise'} || estimate_sunrise($yesterday);
      $slice_start = $data->{$yesterday}->{'sunset'} || estimate_sunset($yesterday);
      $slice_end = $data->{$today}->{'sunrise'};
      $starting_hour = 6;
    } elsif ($t < $data->{$today}->{'sunset'}) {
      $daybreak = $data->{$today}->{'sunrise'};
      $slice_start = $data->{$today}->{'sunrise'};
      $slice_end = $data->{$today}->{'sunset'};
      $starting_hour = 1;
    } else {
      $daybreak = $data->{$today}->{'sunrise'};
      $slice_start = $data->{$today}->{'sunset'};
      $slice_end = $data->{$tomorrow}->{'sunrise'};
      $starting_hour = 6;
    }
    if ($slice_start && $slice_end) {
      no integer;
      my $slice = ($slice_end - $slice_start)/5;
      $deciday = $starting_hour + ($t - $slice_start)/$slice;
    }
  }
  if (@data) {
    my($chk, $stem, $branch, $animal) = ($1, $2, $3, $4) if ($enable_chinese? $header =~ /(\d+)\((.)(.)\s*-\s*肖(.)\)/: $header =~ / of (\d+)\s*\((\w+)-(\w+)\s.*?year of the (\w+)\)/i);
    #2016年1月1日          廿二        星期五
    #2016年1月2日          廿三        星期六
    #2016年1月3日          廿四        星期日
    #2016年1月4日          廿五        星期一
    #2016年1月5日          廿六        星期二
    if (!defined $animal) {
      warn "Internal error: Can't find animal in conversion table\n";
    } elsif ($chk != $year + 1900) {
      die "Internal error: Unexpected year $chk in conversion table\n";
    } else {
      # Figure out the primary year number of the conversion table
      my $reference_year_number;
      for (my $det = 0; $det < 60; $det += 1) {
	my($i, $j) = ($det%10, $det%12);
	$reference_year_number = $det + 1 if lc $stems[$i]->{$lang} eq lc $stem && lc $branches[$j]->{$lang} eq lc $branch;
      last if defined $reference_year_number;
      }

      # Find the column alignments
      my $c_greg = index($headings, $enable_chinese? '公曆日期': 'Gregorian date');
      my $c_lunar = index($headings, $enable_chinese? '農曆日期': 'Lunar date');
      my $c_wday = index($headings, $enable_chinese? '星期': 'Day-of-week');
      my $c_term = index($headings, $enable_chinese? '節氣': 'Solar terms');

      die "Internal error: Cannot find Gregorian date heading\n" unless defined $c_greg;
      die "Internal error: Cannot find Lunar date heading\n" unless defined $c_lunar;
      die "Internal error: Cannot find Day-of-week heading\n" unless defined $c_wday;
      die "Internal error: Cannot find Solar terms heading\n" unless defined $c_term;

      my $w_greg = $c_lunar - $c_greg;
      my $w_lunar = $c_wday - $c_lunar;
      my $w_wday = $c_term - $c_wday;

      # Find today's date in the conversion table
      my $target = sprintf(($enable_chinese? '%d年%d月%d日': '%d/%d/%d'), $year + 1900, $mon + 1, $mday);
      my($current_year, $current_month, $current_day);
      my($year, $month, $day, $solar_term);
      for my $data (@data) {
	my $greg_i = trim substr($data, $c_greg, $w_greg);
	my $lunar_i = trim substr($data, $c_lunar, $w_lunar);
	my $wday_i = trim substr($data, $c_wday, $w_wday);
	my $solar_i = trim substr($data, $c_term);
	print STDERR "DEBUG: greg_i=($greg_i), lunar_i=($lunar_i), wday_i=($wday_i), solar_i=($solar_i)\n" if $debug;
      last if defined $year && defined $month && defined $day;
	if ($lunar_i =~ /^\d+$/) {
	  $current_day = $lunar_i + 0;
	} elsif ($lunar_i =~ /^(\d+)(?:st|nd|rd|th) Lunar Month/) {
	  $current_month = $1 + 0;
	  $current_year = $reference_year_number if $current_month == 1;
	} elsif ($lunar_i =~ /^初(\S+)/ || $lunar_i =~ /^((?!初)\S+)(?<!月)$/) {
	  $current_day = parse_chinese_number $1;
	  print STDERR "DEBUG: input day ($1), parsed $current_day\n" if $debug;
	} elsif ($lunar_i =~ /^(\S+)月/) {
	  $current_month = ($1 eq '正')? 1: parse_chinese_number $1;
	  $current_year = $reference_year_number if $current_month == 1;
	}
	($year, $month, $day, $solar_term) = ($current_year, $current_month, $current_day, $solar_i) if $greg_i eq $target;
	$month = $current_month - 1 if defined $day && !defined $month && defined $current_month;
	$year = $current_year - 1 if defined $day && !defined $year && defined $current_year;
      }
      undef $solar_term unless $solar_term;
      my $chinese_hour = (($hour + 1)%24)/2 + 1;
      my $quarter = $min/15 + 1;
      my $centiday = eval { no integer; ($t - $midnight)/864 + 1 };
      $quarter += 4 if $hour%2 == 0;
      $it = [$year, $month, $day, $solar_term, $chinese_hour, $quarter, $centiday, $deciday];
      ((3+1)%24)/2+1
    }
  }
  return $it;
}

sub format_chinese_date ($) {
  my($data) = @_;
  my $it;
  my($year, $month, $day, $solar_term, $chinese_hour, $quarter, $centiday, $deciday) = @$data;
  if (defined $year) {
    my $minute = eval { no integer; ($centiday*60)%60 };
    my $decaminute = $minute/10;
    $minute %= 10 if $enable_decaminutes;
    if ($enable_chinese && !$sysline_mode) {
      $it = sprintf '%s(%s)年%s月%s日', stem_branch_name($year), get_representation($branches[($year - 1)%12]), $month, $day;
      if ($enable_decaminutes) {
	$it .= sprintf '%d刻', $centiday;
	$it .= sprintf '%d小刻', $decaminute if $decaminute;
	$it .= sprintf '%d分', $minute if $minute && $enable_minutes;
      } elsif ($enable_minutes) {
	$it .= sprintf '%d刻', $centiday;
	$it .= sprintf '%d分', $minute if $minute;
      } else {
	$it .= sprintf '%.*f刻', $decimals, $centiday;
      }
      $it .= sprintf ' (%s)', $solar_term if defined $solar_term;
      $it .= sprintf '(%s時', $branches[$chinese_hour - 1]->{$lang};
      $it .= $quarter < 5? '初': '正';
      if ($enable_qing_quarters) {
	$it .= ('初', '一', '二', '三')[($quarter - 1)%4];
	$it .= '刻';
      }
      $it .= ')';
      if ($chinese_hour >= 11 || $chinese_hour <= 3) {
	$it .= sprintf('(%d更', (($chinese_hour - 10) + 12)%12);
	my $subwatch = eval { no integer; int(($quarter - 1)*5/8) };
	$it .= sprintf('%d點', $subwatch) if $subwatch;
	$it .= ')';
      }
    } else {
      $it = sprintf '%s/%s/%s', $year, $month, $day;
      if ($enable_decaminutes) {
	$it .= sprintf '+%d°', $centiday;
	$it .= sprintf "%d'", $decaminute;
	$it .= sprintf '%d"', $minute if $enable_minutes;
      } elsif ($enable_minutes) {
	$it .= sprintf '+%d°', $centiday;
	$it .= sprintf "%d'", $minute;
      } else {
	$it .= sprintf '+%.*f°', $decimals, $centiday;
      }
      if (!$sysline_mode) {
	$it .= sprintf(' (%s, %s)', stem_branch_name($year), get_representation($branches[($year - 1)%12]));
      }
      $it .= sprintf ' (%s)', $solar_term if defined $solar_term;
      if ($enable_qing_quarters) {
	$it .= sprintf ' %d%s%d', $chinese_hour, ($quarter < 5? 'a': 'b'), ($quarter - 1) % 4;
      }
      if (!$sysline_mode) {
	$it .= sprintf ' (%s)', $branches[$chinese_hour - 1]->{$lang};
      }
      if ($chinese_hour >= 11 || $chinese_hour <= 3) {
	my $watch = (($chinese_hour - 10) + 12)%12;
	$it .= $sysline_mode? sprintf('(%d)', $watch): sprintf('(%s watch)', ordinal($watch));
      }
    }
    if (defined $deciday) {
      if ($sysline_mode) {
	$it .= sprintf(' %s+%d', ($deciday < 6? 'd': 'e'), ($deciday - 1)%5);
      } elsif ($enable_chinese) {
	$it .= sprintf(' %s:%s', ($deciday < 6? '晝': '夜'), (qw(朝 禺 中 晡 夕 甲 乙 丙 丁 戊)[$deciday - 1]));
      } else {
	$it .= sprintf(' %s+%d', ($deciday < 6? 'morning': 'evening'), ($deciday - 1)%5);
      }
    }
  }
  return $it;
}

# A useless system described by https://en.wikipedia.org/wiki/Planetary_hours
# and http://www.renaissanceastrology.com/3rdplanetaryorder.html
# Alas, according to http://www.chabad.org/calendar/zmanim_cdo/aid/143790/jewish/Halachic-Times-Zmanim.htm
# and http://www.agapebiblestudy.com/charts/jewishtimedivision.htm
# this weird system is actually identical to the Hebrew system :-/
sub determine_planetary_hour () {
  my $it;
  my $data = read_xfce_cache;
  if (defined $data) {
    my $t = time;
    my $today = start_of_day $t;
    my $yesterday = yesterday $t;
    my $tomorrow = tomorrow $t;
    my $det = $data->{$today};
    my($daybreak, $slice_start, $slice_end, $starting_hour);
    if ($t < $data->{$today}->{'sunrise'}) {
      $daybreak = $data->{$yesterday}->{'sunrise'} || estimate_sunrise($yesterday);
      $slice_start = $data->{$yesterday}->{'sunset'} || estimate_sunset($yesterday);
      $slice_end = $data->{$today}->{'sunrise'};
      $starting_hour = 13;
    } elsif ($t < $data->{$today}->{'sunset'}) {
      $daybreak = $data->{$today}->{'sunrise'};
      $slice_start = $data->{$today}->{'sunrise'};
      $slice_end = $data->{$today}->{'sunset'};
      $starting_hour = 1;
    } else {
      $daybreak = $data->{$today}->{'sunrise'};
      $slice_start = $data->{$today}->{'sunset'};
      $slice_end = $data->{$tomorrow}->{'sunrise'};
      $starting_hour = 13;
    }
    if ($slice_start && $slice_end) {
      no integer;
      my $slice = ($slice_end - $slice_start)/12;
      my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime $daybreak;
      $it = [$starting_hour + ($t - $slice_start)/$slice, $wday, $t];
    }
  }
  return $it;
}

# A useless system described by https://en.wikipedia.org/wiki/Planetary_hours
# and http://www.renaissanceastrology.com/3rdplanetaryorder.html
sub format_planetary_hour ($) {
  my($data) = @_;
  my($planetary_hour, $wday, $t) = @$data;
  no integer;
  my $m = int(60*($planetary_hour - 1))%60 + 1;
  my $s = int(3600*($planetary_hour - 1))%60 + 1;
  my $fmt = $enable_seconds? '%02d:%02d:%02d': '%02d:%02d%0.0s';
  my $it = sprintf('%d+', $wday + 1) unless $planetary_omit_details;
  $it .= sprintf($fmt, $planetary_hour, $m, $s) unless $planetary_omit_details;
  my $style = $sysline_mode? 'icon': $representation;
  local $representation = $style;
  $fmt = $sysline_mode? ' %s': ' (%s)';
  $it .= sprintf($fmt, get_representation($planets[($wday + 5*(int($planetary_hour) - 1))%7]));
  return $it;
}

# The Hebrew system uses a different starting time (sunset)
# Cf https://www.britannica.com/science/calendar/Ancient-and-religious-calendar-systems
#    https://zh.wikipedia.org/wiki/希伯來曆
# but apparently it's supposed to use the weird hour divisions too
# Cf http://kehillatisrael.net/hcal/zmanim.html
# Although the day number is supposed to change at 6pm (?!)
# Cf http://www.jewfaq.org/calendr2.htm
sub determine_hebrew_date () {
  my $it;
  my $data = read_xfce_cache;
  if (defined $data) {
    my $t = time;
    my $today = start_of_day $t;
    my $yesterday = yesterday $t;
    my $tomorrow = tomorrow $t;
    my $det = $data->{$today};
    my($daybreak, $slice_start, $slice_end, $starting_hour);
    if ($t < $data->{$today}->{'sunrise'}) {
      $daybreak = $data->{$yesterday}->{'sunset'} || estimate_sunset($yesterday);
      $slice_start = $data->{$yesterday}->{'sunset'} || estimate_sunset($yesterday);
      $slice_end = $data->{$today}->{'sunrise'};
      $starting_hour = 1;
    } elsif ($t < $data->{$today}->{'sunset'}) {
      $daybreak = $data->{$yesterday}->{'sunset'} || estimate_sunset($yesterday);
      $slice_start = $data->{$today}->{'sunrise'};
      $slice_end = $data->{$today}->{'sunset'};
      $starting_hour = 13;
    } else {
      $daybreak = $data->{$today}->{'sunset'};
      $slice_start = $data->{$today}->{'sunset'};
      $slice_end = $data->{$tomorrow}->{'sunrise'};
      $starting_hour = 1;
    }
    if ($slice_start && $slice_end) {
      no integer;
      my $slice = ($slice_end - $slice_start)/12;
      print STDERR "DEBUG: daybreak=$daybreak\n" if $debug;
      my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) = localtime($daybreak); # pick something unambiguous
      my $hebcal = -f '/usr/bin/hebcal'? '/usr/bin/hebcal': '/usr/local/bin/hebcal';
      my @cmd = ($hebcal, '-T',
	  '-l', eval { no integer; sprintf('%d,%d', $lat, int(60*$lat)%60) },
	  '-L', eval { no integer; sprintf('%d,%d', $lon, int(60*$lon)%60) }, $mon + 1, $mday, $year + 1900);

      print STDERR 'DEBUG: running: ', join(' ', @cmd), "\n" if $debug;
      my $pid = open(INPUT, '-|');
      my($year, $month, $day);
      if (defined $pid && $pid) {
	my $s = scalar <INPUT>;
	($year, $month, $day) = ($3, $2, $1) if $s =~ /^(\d+)\w* of (.*?), (\d+)$/;
	close INPUT;
      } elsif (defined $pid) {
	exec {$cmd[0]} @cmd;
	die "$0: determine_hebrew_date: exec: $!\n";
      }
      $it = [$year, $month, $day, $starting_hour + ($t - $slice_start)/$slice, $wday, $t];
    }
  }
  return $it;
}

# day goes first, then momth, then year
# cf. https://www.hebcal.com/converter/
sub format_hebrew_date ($) {
  my($data) = @_;
  my($year, $month, $day, $hour) = @$data;
  my $it;
  print STDERR "DEBUG: year=$year, month=$month, day=$day, hour=$hour\n" if $debug;
  if ($sysline_mode) {
    $it = sprintf('%d/%d/%d %s:%d', $year, $hebrew_months{$month}->{'xx'}, $day, ($hour < 13? 'e': 'd'), ($hour - 1)%12 + 1);
    if ($hour <= 12) {
      $it .= sprintf('(%d)', ordinal(($hour - 1)/3 + 1));
    }
  } elsif ($enable_chinese) {
    $it = sprintf('%d年%s月%d日%s間%d時', $year, $hebrew_months{$month}->{$lang}, $day, ($hour < 13? '夜': '日'), ($hour - 1)%12 + 1);
    if ($hour <= 12) {
      $it .= sprintf('(%d更)', ordinal(($hour - 1)/3 + 1));
    }
  } else {
    $it = sprintf('%d/%s/%d %dh (%s)', $day, $hebrew_months{$month}->{$lang}, $year, ($hour - 1)%12 + 1, ($hour < 13? 'night': 'day'));
    if ($hour <= 12) {
      $it .= sprintf('(%s watch)', ordinal(($hour - 1)/3 + 1));
    }
  }
  return $it;
}

sub usage (;$) {
  my($st) = @_;
  my $h = $st? *STDERR: *STDOUT;
  print $h <<EOF;
Usage: $0 [OPTION]...
Display date and time information in a non-mainstream calendar.

  -d, --decimals=N            Display N decimal places for fractional ancient
                              Chinese centidays (used only when decaminutes
                              are not enabled)
  -M, --decaminutes           Enable display of ancient Chinese decaminutes
                              (and disable display of fractional centidays)
  -m, --minutes               Enable display of Western "planetary" minutes (and
                              ancient Chinese minutes if decaminutes are enabled)
  -s, --seconds               Enable display of Western "planetary" seconds
  -Q, --enable-qing-quarters  Use the Qing quarters system (identical to modern
                              quarters) instead of the ancient centiday system
  -p, --enable-planetary=all  Enable the Western "planetary" system and display
                              current time in addition to the equivalent zodiac
  -p, --enable-planetary=minimal
                              Enable the Western "planetary" system but display
                              only the zodiac corresponding to the current hour
  -w, --sysline               Terse format suitable for use as a sysline .who file
      --iconic                Use zodiac symbols
      --symbolic              Use zodiac names
      --debug                 Produce debugging output

Note that Chinese and Hebrew dates and times are always shown. Western "planetary"
times are shown only when enabled.
EOF
  exit $st;
}

Getopt::Long::config('bundling');
GetOptions(
  'debug' => \$debug,
  'd|decimals=i' => \$decimals,
  'M|enable-decaminutes|decaminutes' => \$enable_decaminutes,
  'm|enable-minutes|minutes' => \$enable_minutes,
  's|enable-seconds|seconds' => \$enable_seconds,
  'Q|enable-qing-quarters|qing-quarters|qing' => \$enable_qing_quarters,
  'p|enable-planetary:s' => sub {
	$enable_planetary = 1;
	die "You must specify either \"all\" or \"minimal\"\n" unless $_[1] =~ /^(?:all|full|min(?:imal(?:istic)?)?)/;
	$planetary_omit_details = 1 if $_[1] =~ /^min/;
      },
  'w|sysline' => \$sysline_mode,
  'z|zh|chinese|kanji' => \$enable_chinese,
  'iconic' => sub { $representation = 'icon' },
  'symbolic' => sub { $representation = 'name' },
  'help' => \&usage,
) || usage(1);
$lang = $enable_chinese? 'zh': 'en';

binmode STDIN, ':utf8';
binmode STDOUT, ':utf8';
binmode STDERR, ':utf8';

my $c = determine_chinese_date;
my $h = determine_hebrew_date;
my $p = determine_planetary_hour;
print format_chinese_date $c;
print $sysline_mode? ' | ': "\n";
if ($enable_planetary) {
  print format_planetary_hour $p;
  print "| ";
}
if (defined $h) {
  print format_hebrew_date $h;
  print "\n";
} else {
  print STDERR "DEBUG: no Hebrew date data, hebcal(1) might not be present\n" if $debug;
}
